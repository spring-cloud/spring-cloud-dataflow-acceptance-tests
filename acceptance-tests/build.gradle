buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
		junitPlatformVersion = '1.3.2'
		junitJupiterVersion = '5.3.2'
		dockerComposeRuleJunit4Version = '0.33.0'
		//gradleDockerVersion = '0.17.2'
		gradleDockerVersion = '0.20.1'
		// skipper versions
		springCloudSkipper100VersionTag = '1.0.0'
		springCloudSkipper100Version = "${springCloudSkipper100VersionTag}.RELEASE"
		springCloudSkipper101VersionTag = '1.0.1'
		springCloudSkipper101Version = "${springCloudSkipper101VersionTag}.RELEASE"		
		springCloudSkipper102VersionTag = '1.0.2'
		springCloudSkipper102Version = "${springCloudSkipper102VersionTag}.RELEASE"
		springCloudSkipper103VersionTag = '1.0.3'
		springCloudSkipper103Version = "${springCloudSkipper103VersionTag}.RELEASE"
		springCloudSkipper104VersionTag = '1.0.4'
		springCloudSkipper104Version = "${springCloudSkipper104VersionTag}.RELEASE"
		springCloudSkipper105VersionTag = '1.0.5'
		springCloudSkipper105Version = "${springCloudSkipper105VersionTag}.RELEASE"
		springCloudSkipper110VersionTag = '1.1.0'
		springCloudSkipper110Version = "${springCloudSkipper110VersionTag}.RELEASE"
		springCloudSkipper111VersionTag = '1.1.1'
		springCloudSkipper111Version = "${springCloudSkipper111VersionTag}.RELEASE"
		springCloudSkipper112VersionTag = '1.1.2'
		springCloudSkipper112Version = "${springCloudSkipper112VersionTag}.RELEASE"
		springCloudSkipper11xVersionTag = '1.1.3'
		springCloudSkipper11xVersion = "${springCloudSkipper11xVersionTag}.BUILD-SNAPSHOT"
		springCloudSkipper20xVersionTag = '2.0.0'
		springCloudSkipper20xVersion = "${springCloudSkipper20xVersionTag}.BUILD-SNAPSHOT"
		// dataflow versions
		springCloudDataflow173VersionTag = '1.7.3'
		springCloudDataflow173Version = "${springCloudDataflow173VersionTag}.RELEASE"
		springCloudDataflow17xVersionTag = '1.7.4'
		springCloudDataflow17xVersion = "${springCloudDataflow17xVersionTag}.BUILD-SNAPSHOT"
		springCloudDataflow20xVersionTag = '2.0.0'
		springCloudDataflow20xVersion = "${springCloudDataflow20xVersionTag}.BUILD-SNAPSHOT"
		springCloudDataflow21xVersionTag = '2.1.0'
		springCloudDataflow21xVersion = "${springCloudDataflow21xVersionTag}.BUILD-SNAPSHOT"
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://dl.bintray.com/palantir/releases" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("com.palantir.gradle.docker:gradle-docker:${gradleDockerVersion}")
	}
}

def getResolvedVersionOf(configuration, dependency) {
	return configuration.resolvedConfiguration.firstLevelModuleDependencies.findAll{
	it.moduleName == dependency
	}.moduleArtifacts.first()
}

def dataflowLegacyProjects1x() {
	subprojects.findAll { project ->
		project.name.contains('spring-cloud-dataflow-acceptance-image-legacy-1')
	}
}

def dataflowLegacyProjects2x() {
	subprojects.findAll { project ->
		project.name.contains('spring-cloud-dataflow-acceptance-image-legacy-2')
	}
}

def dataflowDriversProjects() {
	subprojects.findAll { project ->
		project.name.contains('spring-cloud-dataflow-acceptance-image-drivers')
	}
}

def skipperLegacyProjects() {
	subprojects.findAll { project ->
		project.name.contains('spring-cloud-skipper-acceptance-image-legacy')
	}
}

def skipperDriversProjects() {
	subprojects.findAll { project ->
		project.name.contains('spring-cloud-skipper-acceptance-image-drivers')
	}
}

configure(allprojects) {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'io.spring.dependency-management'

	group = 'org.springframework.cloud.dataflow.acceptance'

	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url "https://repo.spring.io/release" }
		maven { url "https://dl.bintray.com/palantir/releases" }
		maven {
			url "http://repo.springsource.org/ext-private-local"
			credentials {
				username = System.getenv('ARTIFACTORY_USERNAME') ?: artifactoryUsername
				password = System.getenv('ARTIFACTORY_PASSWORD') ?: artifactoryPassword
			}
		}
	}

	compileJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	dependencyManagement {
		imports {
		}
		dependencies {
			dependency "com.palantir.docker.compose:docker-compose-rule-junit4:$dockerComposeRuleJunit4Version"
			dependency "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
			// to run junit5 in sts
			dependency "org.junit.platform:junit-platform-engine:$junitPlatformVersion"
			dependency "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
			dependency "org.junit.platform:junit-platform-runner:$junitPlatformVersion"
			dependency "org.junit.platform:junit-platform-suite-api:$junitPlatformVersion"
		}
	}
}

configure(dataflowDriversProjects()) { subproject ->
	def prefix = subproject.name.tokenize('-').last()
	def val = rootProject.ext["springCloudDataflow" + prefix + "Version"]
	apply plugin: 'com.palantir.docker'

	dependencies {
		compile("org.springframework.cloud.dataflow.acceptance.app:dataflow-server-with-drivers${prefix}:${val}")
	}

	docker {
		name "springsource-docker-private-local.jfrog.io/scdf/spring-cloud-dataflow-acceptance-image-drivers${prefix}"
		tags "${val}"
		dockerfile file('../spring-cloud-dataflow-acceptance-image-template1/src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.compile, "dataflow-server-with-drivers${prefix}").file.absolutePath[0], '../spring-cloud-dataflow-acceptance-image-template1/src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.compile, "dataflow-server-with-drivers${prefix}").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	if (project.hasProperty('artifactoryDockerPush') && artifactoryDockerPush.toBoolean()) {
		build.dependsOn 'dockerPush'
	}
}

configure(dataflowLegacyProjects1x()) { subproject ->
	def prefix = subproject.name.tokenize('-').last()
	def val = rootProject.ext["springCloudDataflow" + prefix + "Version"]
	apply plugin: 'com.palantir.docker'

	dependencies {
		compile("org.springframework.cloud:spring-cloud-dataflow-server-local:${val}")
	}

	docker {
		name "springsource-docker-private-local.jfrog.io/scdf/spring-cloud-dataflow-acceptance-image-legacy${prefix}"
		tags "${val}"
		dockerfile file('../spring-cloud-dataflow-acceptance-image-template1/src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.compile, "spring-cloud-dataflow-server-local").file.absolutePath[0], '../spring-cloud-dataflow-acceptance-image-template1/src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.compile, "spring-cloud-dataflow-server-local").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	if (project.hasProperty('artifactoryDockerPush') && artifactoryDockerPush.toBoolean()) {
		build.dependsOn 'dockerPush'
	}
}

configure(dataflowLegacyProjects2x()) { subproject ->
	def prefix = subproject.name.tokenize('-').last()
	def val = rootProject.ext["springCloudDataflow" + prefix + "Version"]
	apply plugin: 'com.palantir.docker'

	dependencies {
		compile("org.springframework.cloud:spring-cloud-dataflow-server:${val}")
	}

	docker {
		name "springsource-docker-private-local.jfrog.io/scdf/spring-cloud-dataflow-acceptance-image-legacy${prefix}"
		tags "${val}"
		dockerfile file('../spring-cloud-dataflow-acceptance-image-template1/src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.compile, "spring-cloud-dataflow-server").file.absolutePath[0], '../spring-cloud-dataflow-acceptance-image-template1/src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.compile, "spring-cloud-dataflow-server").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	if (project.hasProperty('artifactoryDockerPush') && artifactoryDockerPush.toBoolean()) {
		build.dependsOn 'dockerPush'
	}
}

configure(skipperDriversProjects()) { subproject ->
	def prefix = subproject.name.tokenize('-').last()
	def val = rootProject.ext["springCloudSkipper" + prefix + "Version"]
	apply plugin: 'com.palantir.docker'

	dependencies {
		compile("org.springframework.cloud.skipper.acceptance.app:skipper-server-with-drivers${prefix}:${val}")
	}

	docker {
		name "springsource-docker-private-local.jfrog.io/scdf/spring-cloud-skipper-acceptance-image-drivers${prefix}"
		tags "${val}"
		dockerfile file('../spring-cloud-dataflow-acceptance-image-template1/src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.compile, "skipper-server-with-drivers${prefix}").file.absolutePath[0], '../spring-cloud-dataflow-acceptance-image-template1/src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.compile, "skipper-server-with-drivers${prefix}").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	if (project.hasProperty('artifactoryDockerPush') && artifactoryDockerPush.toBoolean()) {
		build.dependsOn 'dockerPush'
	}
}

configure(skipperLegacyProjects()) { subproject ->
	def prefix = subproject.name.tokenize('-').last()
	def val = rootProject.ext["springCloudSkipper" + prefix + "Version"]
	apply plugin: 'com.palantir.docker'

	dependencies {
		compile("org.springframework.cloud:spring-cloud-skipper-server:${val}")
	}

	docker {
		name "springsource-docker-private-local.jfrog.io/scdf/spring-cloud-skipper-acceptance-image-legacy${prefix}"
		tags "${val}"
		dockerfile file('../spring-cloud-dataflow-acceptance-image-template1/src/main/resources/Dockerfile')
		files getResolvedVersionOf(configurations.compile, "spring-cloud-skipper-server").file.absolutePath[0], '../spring-cloud-dataflow-acceptance-image-template1/src/main/resources/entrypoint.sh'
		buildArgs(['JAR_FILE': getResolvedVersionOf(configurations.compile, "spring-cloud-skipper-server").file.name[0], 'ENTRYPOINT_FILE': 'entrypoint.sh'])
	}

	if (project.hasProperty('artifactoryDockerPush') && artifactoryDockerPush.toBoolean()) {
		build.dependsOn 'dockerPush'
	}
}

configure(subprojects) { subproject ->
	apply plugin: 'org.springframework.boot'

	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}
	
	test {
		testLogging {
			events 'STARTED', 'PASSED', 'FAILED', 'SKIPPED'
		}
		useJUnitPlatform {
			if (project.hasProperty('dataflowIncludeTags') && dataflowIncludeTags.size() > 0) {
				includeTags = dataflowIncludeTags.split(',')
			}
			if (project.hasProperty('dataflowExcludeTags') && dataflowExcludeTags.size() > 0) {
				excludeTags = dataflowExcludeTags.split(',')
			}
		}
	}
}

project('spring-cloud-dataflow-acceptance-core') {
	dependencies {
		compile('com.palantir.docker.compose:docker-compose-rule-junit4')
		compile('org.junit.jupiter:junit-jupiter-api')
		compile('org.junit.jupiter:junit-jupiter-engine')
		compile('org.junit.jupiter:junit-jupiter-params')

		compile('org.junit.platform:junit-platform-engine')
		compile('org.junit.platform:junit-platform-launcher')
		compile('org.junit.platform:junit-platform-runner')
		compile('org.junit.platform:junit-platform-suite-api')

		testCompile('org.springframework.boot:spring-boot-starter-test')
	}
}

project('spring-cloud-dataflow-acceptance-tests') {
	dependencies {
		compile project(":spring-cloud-dataflow-acceptance-core")
		testCompile('org.springframework.boot:spring-boot-starter-test')
		testCompile('org.springframework.boot:spring-boot-starter-web')
	}

	if (project.hasProperty('artifactoryDockerLocal') && artifactoryDockerLocal.toBoolean()) {
		rootProject.subprojects.findAll {
			it.name.contains('spring-cloud-dataflow-acceptance-image') ||
			it.name.contains('spring-cloud-skipper-acceptance-image')
		}.collect {
			// weird, build.dependsOn ":${it.name}:build" doesn't cause
			// all images to get created before tests run
			test.dependsOn ":${it.name}:dockerTag"
		}
	}
}
